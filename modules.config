/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    withName: SANGER_TOL_BTK {
        ext.args            = ""
        ext.executor        = "bsub -Is -tty -e test.e -o test.log -n 2 -q oversubscribed -M1400 -R'select[mem>1400] rusage[mem=1400] span[hosts=1]'"
        ext.profiles        = "singularity,sanger"
        ext.get_versions    = "lsid | head -n1 | cut -d ',' -f 1"
        ext.version         = "0.6.0"
        publishDir = [
            path: { "${params.outdir}/sanger-tol-btk" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: "AUTOFILTER_AND_CHECK_ASSEMBLY|CREATE_BTK_DATASET|ORGANELLE_CONTAMINATION_RECOMMENDATIONS|FILTER_BARCODE|SUMMARISE_VECSCREEN_OUTPUT|MERGE_BTK_DATASETS" {
        publishDir = [
            path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
            mode: params.publish_dir_mode
        ]
    }

    withName: AUTOFILTER_AND_CHECK_ASSEMBLY {
        publishDir = [
            path: { "${params.outdir}/" },
            mode: params.publish_dir_mode,
            pattern: "autofiltering_done_indicator_file.txt"
        ]
    }

    withName: GenIndicator {
        publishDir = [
            path: { "${params.outdir}/" },
            mode: params.publish_dir_mode
        ]
    }

    withName: ASCC_MERGE_TABLES {
        publishDir = [
            path: { "${params.outdir}/ASCC-main-output" },
            mode: params.publish_dir_mode
        ]
    }

    withName: FILTER_FASTA {
        ext.args    = "--low_pass --remove_original_fasta"
        ext.cutoff  = 1900000000
    }

    withName: SEQKIT_SLIDING {
        ext.args    = {"-s ${meta.sliding} -W ${meta.window} "}
    }

    withName: '.*:.*:EXTRACT_NT_BLAST:BLAST_CHUNK_TO_FULL' {
        ext.args    = "nucleotide"
    }

    withName: '.*:.*:NR_DIAMOND:DIAMOND_BLAST_CHUNK_TO_FULL' {
        ext.args    = "diamond"
    }

    withName: '.*:.*:NR_DIAMOND:CONVERT_TO_HITS_FILE' {
        ext.args    = "nr"
    }

    withName: '.*:.*:UP_DIAMOND:DIAMOND_BLAST_CHUNK_TO_FULL' {
        ext.args    = "diamond"
    }

    withName: '.*:.*:UP_DIAMOND:CONVERT_TO_HITS_FILE' {
        ext.args    = "Uniprot"
    }

    withName: BLAST_MAKEBLASTDB {
        ext.args = { "-dbtype nucl" }
    }

    withName: '.*:.*:BLAST_BLASTN' {
        ext.args        = { '-outfmt "6 qseqid staxids bitscore std" -max_target_seqs 10 -max_hsps 1 -evalue 1e-25 -dust yes -lcase_masking' }
        ext.prefix      = { "ascc-${meta2.id}" }
        ext.dbprefix    = '*'
    }

    withName: DIAMOND_BLASTX {
        ext.args    = { "--sensitive --max-target-seqs 3 --evalue 1e-25 --no-unlink --tmpdir ./" }
    }

    withName: '.*:EXTRACT_NT_BLAST:BLAST_BLASTN_MOD' {
        ext.args        = { '-outfmt "6 qseqid staxids bitscore std" -max_target_seqs 10 -max_hsps 1 -evalue 1e-25 -dust yes -lcase_masking' }
        ext.dbprefix    = { "${meta2.id}" }
    }

    withName: '.*:.*:(PLASTID_ORGANELLAR_BLAST|MITO_ORGANELLAR_BLAST):BLAST_BLASTN' {
        ext.args    = { "-task megablast -word_size 28 -best_hit_overhang 0.1 -best_hit_score_edge 0.1 -dust yes -evalue 0.0001 -perc_identity 80 -soft_masking true -outfmt 7" }
    }

    withName: SAMTOOLS_DEPTH{
        ext.args = { "-aa" }
    }

    withName: SAMTOOLS_SORT{
        ext.prefix  = { "${meta.id}_sorted" }
    }

    withName: KRAKEN2_KRAKEN2 {
        ext.args    = { "--report-zero-counts --use-names --memory-mapping" }
    }

    withName: FCS_FCSADAPTOR_PROK {
        ext.args    = "--prok"
        ext.prefix  = { "${meta.id}_prok" }
    }

    withName: FCS_FCSADAPTOR_EUK {
        ext.args    = "--euk"
        ext.prefix  = { "${meta.id}_euk" }
    }

    withName: "FCS_FCSADAPTOR_EUK|FCS_FCSADAPTOR_PROK" {
        publishDir = [
            path: { "${params.outdir}/FCS-adaptor" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: SED_SED {
        ext.prefix  = { "${meta.id}_fixed" }
        ext.args    = " -e '/>/s/ //g' "
    }

    withName: '.*:.*:GENERATE_GENOME:GNU_SORT' {
        ext.prefix  = { "${meta.id}_sorted"}
        ext.args    = { '-k2,2 -nr' }
    }

    withName: MINIMAP2_ALIGN_SE {
            ext.args = {'-ax '+ (meta.readtype.equals("hifi") ? "map-hifi" : meta.readtype.equals("clr") ? "map-pb" : meta.readtype.equals("ont") ? "map-ont" : "") + ' --cs=short' + (reference.size() > 4e9 ? (" -I" + Math.ceil(reference.size()/1073741824)+"G") : "") }
            ext.prefix  = { "${meta.id}_alignment_${reference.getName().tokenize('.')[0]}" }
    }

    withName: MINIMAP2_ALIGN_ILLUMINA {
            ext.args = { '-ax sr --cs=short' + (reference.size() > 4294967296 ? (" -I" + Math.ceil(reference.size()/1073741824)+"G") : "") }
            ext.prefix  = { "${meta.id}_alignment_${reference.getName().tokenize('.')[0]}" }
    }

    withName: NCBITOOLS_VECSCREEN {
        ext.args = { "-f3" }
    }

    withName: FILTER_VECSCREEN_RESULTS {
        ext.args    =   "--skip_reporting_suspect_hits --skip_reporting_weak_hits --skip_reporting_no_hits"
    }
}
